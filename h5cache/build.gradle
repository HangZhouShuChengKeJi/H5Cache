apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven'

android {
    compileSdkVersion 28


    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    compileOnly 'com.android.support:appcompat-v7:28.0.0'
    compileOnly 'io.reactivex:rxandroid:1.2.1'
    compileOnly 'io.reactivex:rxjava:1.3.4'
    compileOnly 'com.squareup.retrofit2:retrofit:2.5.0'
    compileOnly 'com.google.code.gson:gson:2.8.5'
    compileOnly 'com.squareup.retrofit2:converter-gson:2.4.0'
    compileOnly 'com.squareup.retrofit2:adapter-rxjava:2.3.0'
    compileOnly 'com.orange.note:net:1.1.6'
    compileOnly 'io.reactivex.rxjava2:rxandroid:2.1.0'
    compileOnly 'io.reactivex.rxjava2:rxjava:2.2.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

//打包源代码
task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

uploadArchives {

    def username = ""
    def password = ""

    if (project.rootProject.file('local.properties').exists()) {
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        username = properties.getProperty("maven.username")
        password = properties.getProperty("maven.password")
    }

    repositories {
        mavenDeployer {
            snapshotRepository(url: 'http://maven.91chengguo.com/repository/maven-snapshots/') {
                authentication(userName: username, password: password)
            }
            repository(url: 'http://maven.91chengguo.com/repository/maven-releases/') {
                authentication(userName: username, password: password)
            }
            pom.packaging = 'aar'
            pom.version = "1.0.1"
            pom.groupId = 'com.orange.note'
            pom.artifactId = "h5cache"
        }
    }

}